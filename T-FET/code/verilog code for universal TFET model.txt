// VerilogA for tfet, ntfet, veriloga

`include "discipline.h"

`define n_type 1				// defining the n type
`define p_type -1				// defining the p type
`define CHARGE 1.6021918e-19			// elementary charge
`define M0 9.1095e-31				// Electron rest mass
`define H 6.62606957e-34			// Planck constant
`define HBAR 1.05458e-34			// Reduced Planck constant
`define KB 1.3806488E-23			// Boltzmann constant
`define PI 3.141592653				// Pi value
`define EPS0 8.85418782e-12			// Permittivity of vaccum
`define VDSMIN 1e-12				// minimum Vds for calculations
`define DELTA 5					// Transition width parameter
`define VMIN 0.001				// minimum Voltage for calculations
`define AMIN 1u					// minimum value for capacitance calculations

module ntfet(drain, gate, source);
  inout drain, gate, source;
  electrical drain, gate, source;
  electrical drainprime, gateprime, sourceprime;

// instance parameters
  parameter real w = 1e-6;			// Device Channel Width parameter 
  parameter real l = 2e-8;			// Device Channel Length parameter
  
//drain-source current parameters
  parameter real e0 = 0.527e8;			// Built-in-electric field
  parameter real eg = 0.35;			// Semiconductor bandgap
  parameter real eta = 0.1;			// NDR drain-source voltage sensitivity parameter
  parameter real gamma0 = 0.06;			// Saturation shape parameter
  parameter real j0 = 1e7;			// P-N junction saturation current density
  parameter real jp = 2e8;			// NDR current density parameter
  parameter real k = 2;				// NDR current scale factor
  parameter real k0 = 0;			// parameter for Threshold voltage calculations
  parameter real k1 = 1;			// parameter for Threshold voltage calculations
  parameter real lambda = 0.19;			// Saturation voltage parameter
  parameter real mr = 0.012;			// Reduced effective mass
  parameter real n1 = 1.8;			// Sub-threshold ideality factor
  parameter real n2 = 1.1;			// Ideality factor of RTD
  parameter real r0 = 0.5;			// Tunneling window parameter
  parameter real r1 = 0.01;			// Electrical field parameter
  parameter real r2 = 1.3;			// Electrical field parameter
  parameter real rdw = 0;			// Drain access resistance per unit width
  parameter real rgwl = 0;			// Gate access resistance per gate square
  parameter real rsw = 0;			// Source access resistance per unit width
  parameter real s = 1;				// Ambipolar current attenuation
  parameter real tch = 5e-9;			// Channel thickness
  parameter real voff = 0.01;			// Minimum valid gate-source voltage
  parameter real vp = 0.05;			// NDR parameter
  parameter real vth = 0.17;			// Threshold voltage
 
 //fixed capacitances parameters
  parameter real alpha = 1.14;			// Gate-drain capacitance parameter
  parameter real beta = 0.02;			// Gate-drain capacitance parameter
  parameter real cgs0 = 6.9e-11;  		// Gate-source capacitance per unit width
  parameter real cgdew = 0;			// Extrinsic gate-drain capacitance per unit gate width
  parameter real cgsew = 0;			// Extrinsic gate-source capacitance per unit gate width
  parameter real eot = 0.2n;			// Equivalent oxide thickness
  parameter real gamma1 = 0;			// Saturation shape parameter
  parameter real gammac = 0.18;			// Capacitance parameter
  parameter real mc = 2;			// Cgd knee shape parameter
  parameter real epsi = 3.9;			// Gate insulator dielectric constant
  parameter integer type = `n_type;		// Set the device type
  
  real vds, vsd, vdse, vsde, vgs, vgd, mrvalue, egvalue, u0, a, b, ag, bg, eps, ru, gamma, vthds, vthdsa, gi, r0p, deltas;
  real vgt, vgo, vgoe, vgoen, f, u, e, id;
  real vgta, vgoa, vgoea, vgoena, fa, ua, ea, ida, idr;
  real rd, rg, rs, gd, gg, gs;
  real cgde, cgse;
  real ci, cgs, cgd, cgdmax, cgdmin, ac, ace, vgse;

  analog
    begin
      @(initial_step or initial_step("static"))
        begin
	// minor calculations for operation modes equations
          u0 = n1*$vt;
          mrvalue = mr*`M0;
          egvalue = eg*`CHARGE;
          ru = r0*u0;
          r0p = 1-r0;
          deltas = `DELTA*`DELTA;
          a = w*tch*`CHARGE*`CHARGE*`CHARGE/(8*`PI*`PI*`HBAR*`HBAR)*sqrt(2*mrvalue/egvalue);
          b = 4*egvalue*sqrt(2*mrvalue*egvalue)/(3*`CHARGE*`HBAR);
          eps = epsi*`EPS0;
          cgde = cgdew*w*1e6;
          cgse = cgsew*w*1e6;
          ci = `EPS0*epsi*w*l/eot;
          cgdmax = 0.9*ci;
          cgdmin = 0.13*ci;
	// Parasitic components for extrinsic model calculations
          rd = rdw/(w*1e6);
          rg = rgwl*w/l;
          rs = rsw/(w*1e6);
          if(rd > 0)
            gd = 1/rd;
          else
            gd = 0;
          if(rg > 0)
            gg = 1/rg;
          else
            gg = 0;
          if(rs > 0)
            gs = 1/rs;
          else
            gs = 0;
        end
      vds = type*V(drainprime,sourceprime);
      vgd = type*V(gateprime,drainprime);
      vgs = type*V(gateprime,sourceprime);
      vgt  = vgs-vth;
      vdse = `VDSMIN*(0.5*vds/`VDSMIN+sqrt(deltas+(0.5*vds/`VDSMIN-1)*(0.5*vds/`VDSMIN-1))-sqrt(deltas+1));
      
// main drain-source tunneling current
      vgo  = vgs-voff;
      vgoe = `VMIN*(1+0.5*vgo/`VMIN+sqrt(deltas+(0.5*vgo/`VMIN-1)*(0.5*vgo/`VMIN-1)));
      vgoen = vgoe/(vth-voff);
      gamma = gamma0 + gamma1*vgoe;
      gi = 1/gamma;
      vthds = lambda*tanh(k0+k1*vgo);
      f = (1-limexp(-vds*gi))/(1+limexp((vthds-vds)*gi));
      u = ru+r0p*u0*vgoen;
      e = e0*(1+r1*vds+r2*vgoe);
      id = a*f*u*ln(1+limexp((vgt)/u))*e*limexp(-b/e);
     
 // ambipolar drain-source current
      vgta = -vgs+2*voff-vth;
      vgoa  = -vgo;
      vthdsa = lambda*tanh(k0+k1*vgoa);
      vgoea = `VMIN*(1+0.5*vgoa/`VMIN+sqrt(deltas+(0.5*vgoa/`VMIN-1)*(0.5*vgoa/`VMIN-1)));
      vgoena = vgoea/(vth-voff);
      fa = (1-limexp(-vdse*gi))/(1+limexp((vthdsa-vdse)*gi));
      ua = ru+r0p*u0*vgoena;
      ea = e0*(1+r1*vdse+r2*vgoea);
      ida = s*a*fa*ua*ln(1+limexp((vgta)/ua))*ea*limexp(-b/ea);
      
 // NDR drain-source current
      vsd = -vds;
      vsde = vdse;
      idr = -w*tch*(jp*(vsde/vp)*k*vgoe*limexp(1+(-vsde+eta*vgs)/vp) + j0*(limexp(vsd/n2/$vt)-1));
      id = id + ida + idr;
    
 //capacitance calculations
      vgse = `VMIN*(1+0.5*vgs/`VMIN+sqrt(deltas+(0.5*vgs/`VMIN-1)*(0.5*vgs/`VMIN-1)));
      ac = ((1+beta*pow(vgse,mc))-limexp(-vgoe/gammac))/(1+limexp((vth+alpha*vdse-vgoe)/gammac));
      ace = `AMIN*(1+0.5*ac/`AMIN+sqrt(deltas+(0.5*ac/`AMIN-1)*(0.5*ac/`AMIN-1)));
      cgs = cgs0*w;
      cgd = cgdmin + (cgdmax - cgdmin)*ace;
     
 // Augment the matrix
      I(gateprime,sourceprime) <+ type*cgs*ddt(vgs);			// Intrinsic model branches
      I(gateprime,drainprime) <+ type*cgd*ddt(vgd);			// Intrinsic model branches
      I(drainprime,sourceprime) <+ type*id;				// Intrinsic model branches
      I(gate,drain) <+ type*(ddt(cgde*V(gate,drain)));			// Extrinsic model branches
      I(gate,source) <+ type*ddt(cgse*V(gate,source));			// Extrinsic model branches
 // Extrinsic model calculations
	if(rd > 0)
        begin
          I(drain,drainprime) <+ gd*V(drain,drainprime);
        end
      else
        V(drain,drainprime) <+ 0.0;
      if(rs > 0)
        begin
          I(source,sourceprime) <+ gs*V(source,sourceprime);
        end
      else
        V(source,sourceprime) <+ 0.0;
      if(rg > 0)
        begin
          I(gate,gateprime) <+ gg*V(gate,gateprime);
        end
      else
        V(gate,gateprime) <+ 0.0;
    end
endmodule
